#lang htdp/isl+
(require 2htdp/image)

(define SMALL 8) ; a size measure in terms of pixels

(define small-triangle (triangle SMALL 'outline 'red))

; Number -> Image
; generative creates Sierpinski triangle of size side by generating
; one for (/ side 2) and placing one copy above two copies

(check-expect (sierpinski SMALL) small-triangle)
(check-expect (sierpinski (* 2 SMALL))
              (above small-triangle
                     (beside small-triangle small-triangle)))

(define (sierpinski side)
  (cond
   [(<= side SMALL) (triangle side 'outline 'red)]
   [else
    (local ((define half-sized (sierpinski (/ side 2))))
      (above half-sized (beside half-sized half-sized)))]))